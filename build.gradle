plugins {
    id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "2.3.0"
}

version = project.mod_version + "+" + project.supported_minecraft_versions
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven { url = "https://maven.azureaaron.net/releases" }
        }

        filter {
            includeGroup("net.azureaaron")
        }
    }

    exclusiveContent {
        forRepository {
            maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
        }

        filter {
            includeGroup("me.djtheredstoner")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    include modImplementation("net.azureaaron:hm-api:${project.hm_api_version}")
    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${project.dev_auth_version}")
}

processResources {
    filesMatching("fabric.mod.json") {
        expand(
                "version": project.version,
                "loader_version": project.minimum_loader_version,
                "minecraft_version": project.minimum_minecraft_version,
                "fabric_api_version": project.minimum_fabric_api_version,
                "fabric_kotlin_version": project.fabric_kotlin_version,
                "java_version": 21
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = 21
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    //    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
